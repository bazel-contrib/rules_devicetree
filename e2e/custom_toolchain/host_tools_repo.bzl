# Copyright (C) 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""Auto-detected toolchain from host (user version)."""

_TOOLCHAIN_TOOLS = [
    "dtc",
]

visibility("//devicetree/...")

def _host_tools_repo_impl(repository_ctx):
    found_tools = []
    for tool_name in _TOOLCHAIN_TOOLS:
        tool_path = repository_ctx.which(tool_name)
        if tool_path:
            repository_ctx.symlink(tool_path, tool_name)
            found_tools.append(tool_name)

    build_content = """\
# Generated by e2e/custom_toolchain/host_tools_repo.bzl
exports_files({repr_found_tools})
""".format(
        repr_found_tools = repr(found_tools),
    )

    repository_ctx.file("BUILD.bazel", build_content)

    repository_ctx.file("REPO.bazel", "")

host_tools_repo = repository_rule(
    implementation = _host_tools_repo_impl,
)
